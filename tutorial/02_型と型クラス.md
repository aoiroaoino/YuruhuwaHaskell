#型と型クラス
Tutorial for ゆるふわHaskell入門会

-----------------------------------------

###型
Haskellはとても強力な型システムを持った、静的型付け言語です。  
例えば、次のコードはコンパイル時にエラーになります。
```haskell
five = "five"
three = 3
tryadd = five + three
```
そのため、GHCiで読み込もうとすると、次のようして失敗します。
```haskell
GHCi, version 7.6.1: http://www.haskell.org/ghc/  :? for help
Loading package ghc-prim ... linking ... done.
Loading package integer-gmp ... linking ... done.
Loading package base ... linking ... done.
[1 of 1] Compiling Main             ( Sample.hs, interpreted )

Sample01.hs:2:9:
    No instance for (Num [Char]) arising from the literal `3'
    Possible fix: add an instance declaration for (Num [Char])
    In the expression: 3
    In an equation for `three': three = 3
Failed, modules loaded: none.
Prelude> 
```

####値の型を調べる
GHCiで`:t`コマンドを使うことによって、値が持つ型を見る事ができます。  
`::`の後ろに表示されているのがその値の持っている型です。
```haskell
Prelude> :t True
True :: Bool
Prelude> :t False
False :: Bool
Prelude> :t 'a'
'a' :: Char
Prelude> :t 'X'
'X' :: Char

```
ところで、数値リテラルの型を調べてみると、ちょっと違った表示になります。
```haskell
Prelude> :t 1
1 :: Num a => a
Prelude> : 5
5 :: Num a => a
```
ここで表示されている`Num`というのは「型クラス」、`a`というのは「型変数」です。あとで説明します。

####リスト／タプルの型
リストの型は型名を`[ ]`でかこって表されます。
```haskell
Prelude> :t [True, False]
[True, False] :: [Bool]
Prelude> :t ['a', 'b', 'c']
['a', 'b', 'c'] :: [Char]
Prelude> :t [[True, True], [False, False]]
[[True, True], [False, False]] :: [[Bool]]
```
タプルの型は型名を `( )`でかこって、型名をカンマで区切って表されます。
```haskell
Prelude> :t ('a', True)
('a', True) :: (Char, Bool)
Prelude> :t ("hoge", [True,False,False])
("hoge", [True,False,False]) :: ([Char], [Bool])
```

####式の結果の型を調べる
`:t`コマンドでは、評価前の式から、評価した後の型を得る事ができます。これは凄く便利です。
```haskell
Prelude> :t "Hello" ++ "World!!" 
"Hello" ++ "World!!" :: [Char]
Prelude> :t fst ("hoge", [1,2,3])
fst ("hoge", [1,2,3]) :: [Char]
Prelude> :t zip "ABCD" ["Apple", "Bird", "Car", "Dog"]
zip "ABCD" ["Apple", "Bird", "Car", "Dog"] :: [(Char, [Char])]
```

####よく使う型
|型名    |説明                                               |主な値                   | 
|--------|---------------------------------------------------|-------------------------| 
|Int     |符号付整数(-2147483648〜2147483647)                |-1, 0, 1, 2...           | 
|Integer |Intで収まらないサイズの（範囲制限の無い）符号付整数|-1, 0, 1, 2...           | 
|Float   |単精度浮動小数点数                                 |-1.0, 0.0, 1.5, 2.0 等...| 
|Double  |倍精度浮動小数点数                                 |-1.0, 0.0, 1.5, 2.0 等...| 
|Char    |文字型(文字コードの話はややこしいので割愛)         |'a', 'b', 'c'...         | 
|Bool    |真偽値                                             |TrueまたはFalse          | 

等、実際にはもっともっと沢山の型があり、また、自分で新たな型を定義する事もできますが、第一回ではそこまでやりません。

-----------------------------------------

###型クラス
型クラスはHaskellでポリモフィズム(多態性、多相性)を実現するためのとても重要な機能です。  
今日の勉強会で一番ややこしい概念なのでじっくりやります、ちょっと大変ですけど付いてきてください。

####型クラスのイメージ
型クラスは「OOのInterface」と似ている、と言われていますが、もっと強力です。

以下はごく一部の型と型クラスの関係を示したイメージ図です。

<img src="https://lh6.googleusercontent.com/2UTv2lGhUftufQ8BM7qrX08vaWC6CXWSIC_RLa-wtmszda0jaDTaZag2OKX9JwfaYA=w1600"/>

例えば、`Double`型は`Num`型クラスと`Show`クラスに属しますが、`Bounded`型クラスには属していません。
この事を、「`Double`は`Num`型クラス、`Show`型クラスのインスタンス」と言います。(OOのクラスのインスタンスとは意味が違うので注意してください。)

`Num`型クラスの、インスタンスの値は、加算、乗算、減算をする事ができます、`Num`のインスタンスである`Integer`や`Double`型は足し算や引き算をする事が可能です。  
しかし、`Char`型は`Num`クラスのインスタンスでは無いため、加算、乗算、減算しようとすると、型エラーになります。
```haskell
Prelude> 20 * 100
2000
Prelude> 3.14 + 1.414
4.554
*Main> 'c' + 'x'

<interactive>:25:5:
    No instance for (Num Char) arising from a use of `+'
    Possible fix: add an instance declaration for (Num Char)
    In the expression: 'c' + 'x'
    In an equation for `it': it = 'c' + 'x'
```
`Show`型クラスの、インスタンスの値は、値を文字列に変換する`show`関数を使う事ができます。
従って、`Show`型クラスのインスタンスである、`Double`、`Integer`、`Bool`等の方は、`show`関数で、文字列に変換する事ができます。
```haskell
Prelude> show 3.1415
"3.1415"
Prelude> show 9973
"9973"
Prelude> show True
"True"
```
####型や型クラスの情報を得る
`:i`コマンドを使う事によって、その型のさらに詳細な情報を得る事ができます。
```haskell
Prelude> :i Bool
data Bool = False | True 	-- Defined in `GHC.Types'
instance Bounded Bool -- Defined in `GHC.Enum'
instance Enum Bool -- Defined in `GHC.Enum'
instance Eq Bool -- Defined in `GHC.Classes'
instance Ord Bool -- Defined in `GHC.Classes'
instance Read Bool -- Defined in `GHC.Read'
instance Show Bool -- Defined in `GHC.Show'
```
```haskell
*Main> :i Show
class Show a where
  showsPrec :: Int -> a -> ShowS
  show :: a -> String
  showList :: [a] -> ShowS
  	-- Defined in `GHC.Show'
instance (Show a, Show b) => Show (Either a b)
  -- Defined in `Data.Either'
instance Show a => Show [a] -- Defined in `GHC.Show'
instance Show Ordering -- Defined in `GHC.Show'
instance Show a => Show (Maybe a) -- Defined in `GHC.Show'

...以下、Show型クラスのインスタンスがズラズラっと...
```

####型変数
さて、改めて`:t`コマンドで数値の値を見てみましょう。
```haskell
*Main> :t 5
5 :: Num a => a
```
この a のように、型の中に小文字から始まる文字列が入ってきた場合、それを「型変数」と言います。

`5`というリテラルだけでは、その型が`Integer`なのか、`Int`なのか解りません。そこで、`a`という「型の変数名」を使って、「あらゆる型に成りうる」事を表しているわけです。  
しかし、`5`という式は「あらゆる型に成りうる」わけではありません。そこで、`Num a`という部分で、「`a`は`Num`クラスのインスタンスである」と制約をかけているワケです。

<img src="https://lh4.googleusercontent.com/Mh5QSgTwX7-5Y2N3GCmV1ImIHwptIT3xPNHmHMyXF0_W5jpP9TlttgxKooa6afH1tg=w1600"/>

では、小数の型はどうなっているでしょうか？
```haskell
*Main> :t 0.5
0.5 :: Fractional a => a
```
`Double`や`Float`等の小数は、`Fractional`型クラスのインスタンスです、`:i`コマンドを使って試してみてください。

これだけでは型変数についてピンと来ない方も多いかと思いますので、もう一つ例を出してみましょう。
```haskell
*Main> :t (991, 1.414)
(991, 1.414) :: (Fractional t1, Num t) => (t, t1)
```
t1やtも型変数です。型変数の名前は何でも良いので、次のようにa, bとして、この型の意味を考えてみましょう。

<img src="https://lh6.googleusercontent.com/Ry99mxa_y83TY9rDTBaTeS-Z1dssHe9RaVx3lF0uTiKrCXPNDp1oqLRMnt92DAEFXA=w1600"/>
####明示的に型を指定する
先ほど、`:t`コマンドで、`1` や `2.3` 等のリテラルは、`Num`や`Fractional`のインスタンスである「何かの型」である事を確認しました。  
必要であれば、`::`を使う事で、明示的に型を指定する事ができます。
```haskell
*Main> :t 5 :: Int
5 :: Int :: Int
*Main> :t 5 :: Integer
5 :: Integer :: Integer
*Main> :t 3.14 :: Double
3.14 :: Double :: Double
*Main> :t 3.14 :: Float
3.14 :: Float :: Float
```
`::`を使った明示的な型の指定は、式に対して指定する事ができます。
```haskell
*Main> :t zip [1, 2, 3] ['a', 'b', 'c'] :: [(Integer, Char)]
zip [1, 2, 3] ['a', 'b', 'c'] :: [(Integer, Char)]
  :: [(Integer, Char)]
*Main> :t zip [(1 :: Integer), 2, 3] ['a', 'b', 'c']
zip [(1 :: Integer), 2, 3] ['a', 'b', 'c'] :: [(Integer, Char)]
```

####主な型クラス
|型クラス名|説明|代表的な関数|
|----------|----|------------|
|Show|文字列として表示可能な型がインスタンスになる|show|
|Read|文字列から読み取り可能な型がインスタンスになる|read|
|Num|加減乗ができる型がインスタンスになる|(+), (-), (*), abs等...|
|Integral|整数値の除算や剰余等ができる型がインスタンスになる|div, mod等...|
|Fractional|小数としての除算ができる型がインスタンスになる|(/),recip等...|
|Eq|同値関係の判定が可能な型がインスタンスになる|(==), (/=)等|
|Ord|大小関係の判定が可能な型がインスタンスになる|(<), (>), (>=), (<=)等...|
|Bounded|最小値と最大値の存在する型がインスタンスになる|minBound, maxBound|
|Enum|「次の値」「前の値」が計算できる型がインスタンスになる|succ, pred等...|

#####【おまけ】ガチHaskellerはみんな知ってるヤバげな型クラス達  
|型クラス名|説明|代表的な関数|
|---|---|---|
|Functor|この勉強会の目標その１...いわゆる関手|fmap|
|Applicative|この勉強会の目標その２...Functorの上位互換|pure, (<*>)等...|
|Monad|手続きとか副作用とか非決定性計算とか...例のアレ。|return, (>>=)等...|
|Monoid|モノイドという代数的構造|mempty, mappend, mconcat|
|Category|いわゆる圏|id, (.)|
|Arrow|圏論の射|arr, (&&&)等...|

等、実際にはもっともっと沢山の型クラスがあり、また、自分で新たな型クラスを定義する事もできますが、第一回でh(ry

####型の別名の話

Haskellでは、型に別名を付ける事もできます。  
代表的なものでは、`[Char]`の別名を表す`String`がそうです。
```haskell
Prelude> :t ['h', 'o', 'g', 'e'] :: [Char]
['h', 'o', 'g', 'e'] :: [Char] :: [Char]
Prelude> :t ['h', 'o', 'g', 'e'] :: String
['h', 'o', 'g', 'e'] :: String :: String
Prelude> :t "hoge" :: [Char]
"hoge" :: [Char] :: [Char]
Prelude> :t "hoge" :: String
"hoge" :: String :: String
```
普通に`:t`で表示すると、本来の名前である`[Char]`が表示されてしまいますが、関数定義などでは`String`を使うことの方が多いです。覚えておきましょう。

-----------------------------------------

###関数の型
式や値と同じようにして、Haskellでは関数の型も`:t`コマンドで確認する事ができます。（これらは全て同じことだというのは、Haskellを学ぶうちに解ってくると思います。）
```haskell
Prelude> :t and
and :: [Bool] -> Bool
Prelude> :t or
or :: [Bool] -> Bool
```
Haskellではaを取ってbを返すような関数の型は、`->`という記号を使って、`a -> b`のように表します。
この事から、`and`や`or`が、「`Bool`型のリストを取って、`Bool`を返す関数だ」という事が解ります。

実際に、`and`関数や`or`関数を使ってみましょう。
「`Bool`型のリスト」を引数にしてやれば良いんでしたね。
```haskell
Prelude> and [True, True, False]
False
Prelude> or [True, True, False]
True
Prelude> and [True, True, True]
True
Prelude> or [True, True, True]
True
Prelude> and [False, False, False]
False
Prelude> or [False, False, False]
False
```
`(&&)`や`(||)`のように、二つ以上引数を取る関数は、以下のように`->`を使って繋げて書きます。
```haskell
Prelude> :t (&&)
(&&) :: Bool -> Bool -> Bool
Prelude> :t (||)
(||) :: Bool -> Bool -> Bool
```
`(+)`や`(==)`の型を見てみましょう。
```haskell
Prelude> :t (+)
(+) :: Num a => a -> a -> a
Prelude> :t (==)
(==) :: Eq a => a -> a -> Bool
```
型クラスによる制約や型変数が混ざってちょっとややこしいですが、考え方は一緒です。

<img src="https://lh5.googleusercontent.com/CO8JUO-gbse9Zm2EaMMN-MbW7aaF7HFXd21rZ8MH9Sidzxu67DIpUzP92uA4yROXwg=w1600"/>

足し算の演算子`+`が、`Num`クラスのインスタンスである、あらゆる型に対して、同値判定の演算子`(==)`が`Eq`クラスのインスタンスである、あらゆる型に対して適用する事ができるのが、型から読み取る事ができます。

これが、Haskellのポリモフィズムです。

####関数の型からどんな事が読み取れるか

次のような型を持つ関数`f`を考えてみましょう。
```haskell
f :: (Num a, Num b) => a -> b -> a
```
この型からどんな事が読み取れるでしょうか？  
`a`, `b`共に`Num`型クラスの制約がありますが、`a`と`b`は異なる型なので、第一引数と第二引数をお互い足したり掛けたり引いたりする事は出来なさそうです。`b`が`Eq`型クラスのインスタンスというわけでもないので、分岐の条件に使われる事も無さそうです。

結局、二つ目の引数はfの計算には使えなさそうなので、次のように「捨てる」という方法くらいしか思いつかないですね。
```haskell
f x y = x * 2
```
`x`を二倍しているのに深い意味は無いです。`a`が`Num`クラスのインスタンスであるという事は、一つ目の引数に対して加減乗のなんらかの計算がされている「かもしれない」という事を示しています。
例えばコレが、次のような型だったとしたらどうでしょう？
```haskell
g :: a -> b -> a
```
先ほどの関数`f`から型クラスの定義を外しただけですが、これにより「第一引数で渡されてくる値に対して加減乗の計算ができるかどうか解らない」という事になります。
なので、gの実装に足し算や引き算等の関数を使う事ができません。結局、次のようにして、第一引数をそのまま返すしか方法は無さそうです。
```haskell
g x y = x
```
このようにして、関数の型から「どんな実装が考えられるか」という深い議論できるのが、型の強力な言語の強みなのです。

「どんな実装が考えられるか」という議論は裏を返せば「こんな実装はあり得ない」という議論につながります。  
「こんな実装はあり得ない」という話から「〜由来のバグは起こりえない」という確証に繋げる事ができます。

Haskellに触れていくと、すこしづつ、もっと複雑な型定義に触れる事になりますが、それによってHaskeller達は「Haskellでちゃんと型設計をして書いたプログラムは、まぁ、ほぼほぼ、バグも無く動くよね」なんて事を言うようになるわけです。

####[余談]他の言語の型との比較
このような議論は、純粋関数型言語でないとする事ができないのでしょうか、実は他の言語でも、型の設計を工夫すればある程度は可能です。  
改めて、fの型をを見なおしてみましょう。
```haskell
f :: (Num a, Num b) => a -> b -> a
```
これと同じ関数を、メジャーなオブジェクト指向言語で実装する事を考えてみます。
INumというNum型クラスに対応するインターフェイスが存在すると過程して、C#で実装すると、次のような感じになるでしょうか。
```cs
public A f<A, B>(A x, B y)
    where A : INum, B : INum
{
    //実装
}
```
これなら、この関数が参照透過であるという前提で先ほどのような議論をする事が可能ですが、Haskellと比べると可読性に難ありです。

先ほどのHaskellでの例のような議論をするためには、上記のようにジェネリクスを使った実装にするのが良いのですが、実際にはこのような認識はあまり持たれておらず、通常JavaやC#でこのような関数を作ろうとした場合、慣習的に次のような記述になってしまうと思います。
```cs
public INum f(INum x, INum y)
{
    //実装
}
```
さしあたり大きな問題はなさそうに見えますが、型から読み取れる情報はうんと少なくなります。
その証拠に、第一引数と第二引数に同じ型を要求するadd関数を考えた時と、型がまったく一緒になってしまうのです。
```cs
public INum add(INum x, INum y)
{
    if(typeof(x) != typeof(y))
        throw Exception();
    return x + y;
}
```
addメソッドはxとyに同じ型を要求しているのですが、ジェネリクス型を使わないと実行時に型を判別するしかなく、例外をthrowするという危険な手段を講じる必要が出てきてしまいます。  
また、このように抽象クラスやインターフェースを戻り値や引数に使うのは、具象クラスへのキャストが必要になってしまうため、実行時の型エラーの要因になり、本当は好ましく無いのです。

####ちょっとだけカリー化の話
Haskellの関数は「カリー化されて」います。「カリー化」という言葉の意味はこの後「高階関数」の話をする際にもう一度同じ話をする予定なので、今は詳しくは触れません。

(+)の型をもう一度見てみましょう。
```haskell
Prelude> :t (+)
(+) :: Num a => a -> a -> a
```
この(+)はNum型クラスのインスタンスである型aの値を二つ取って、aの値を返す関数なので、`(+) 1 1` の型を`:t`コマンドで調べると次のようになります。
```haskell
*Main> :t (+) 1 1
(+) 1 1 :: Num a => a
*Main> (+) 1 1
2
```
戻り値である、2の型は、Num型クラスのインスタンスであるaなので、正しい結果になっています。
さて、では(+) 1の型を`:t`コマンドで調べるとどうなるでしょうか？

一般的な関数が「カリー化されて」いない言語に慣れた人だと、引数の数が合わないからエラーになるのでは無いかと考えるかもしれません、しかしHaskellではエラーにならず、以下のような結果になります。
```haskell
*Main> :t (+) 1
(+) 1 :: Num a => a -> a
```
`(+) 1`は、それ自体が「`Num`型クラスのインスタンスである`a`を取って、`a`を返す関数」であるという結果になりました。
この事を利用して、「引数を取ってそれに何か数値を足す関数」は次のようにして、束縛変数を使わずに書く事ができます。

次のプログラムを任意のファイルに保存して実行してみてください。
```haskell
f = (+) 1
g = (+) 2
h = (+) 3
```
```haskell
*Main> f 10
11
*Main> g 10
12
*Main> h 10
13
```
束縛変数を使わずに表現できるという事は、コードに現れる「名前」を減らす事ができるため、適切に使えば可読性を向上させる事ができます。
Haskellの関数のこの性質はこの後もバンバン出てくるので、覚えておいてください。

ここで時間があったら `const`関数の説明します。
