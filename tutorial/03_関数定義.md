#関数定義
Tutorial for ゆるふわHaskell入門会

-----------------------------------------

「関数の型からどんな事が読み取れるか」という話をしましたが、どうだったでしょうか。  
単純に「数値なのか文字列なのか、Boolなのか・・・」といった当たり前の話を超えて、関数の内部実装まで切り込んだ深い議論をする事ができる、というのを少しでも実感できたのでは無いしょうか。
 
関数プログラミングに慣れていない人は、「型を中心に考えると様々な事が見えてくる」という点について、まだピンと来ないかもしれません。 
少しでも早くこの感覚に慣れるために、ここから先「関数定義のシンタックス」→「再帰関数」→「高階関数」と話を進めて行きますが、常に「型の話」を交えながら説明していこうと思います。

###型を明記して関数定義／型推論の話
さて、最初のほうで作成した`Sample01.hs`を開いてみてください。  
次のような３つの関数を作成しましたね。
```haskell
dbl x = x * 2

add x y = x + y

five = 5
```
以下のdblのように、`関数名 :: 型`のように書く事によって、明示的に型を指定する事ができます。
```haskell
dbl :: Num a => a -> a
dbl x = x * 2
```
というか、正確には、本来は型を明示する必要があるのですが、「型推論」という機能により、いままで省略して書くことができたワケです。

(問)　`add`, `five`の２関数に型を明記してみて下さい。
それをGHCiで読み込んで、今まで通りに使う事ができるか確認してみましょう。

今後、この勉強会では、全ての関数に型を明記するようにしましょう。  
できれば、実装を書く前に、型を書くように意識すると良いです。

-------------------------------------------------

###パターンマッチ
Haskellで条件分岐を実現する方法の一つとして、if式を使う方法があります。
例えば、引数が 0 の場合のみTrueを返す関数isZeroは次のようにして実装できます。
```haskell
isZero :: (Eq a, Num a) => a -> Bool
isZero x = if x == 0 then True else False 
```
しかし、引数に対する条件分岐は、大抵の場合ifを使うよりパターンマッチを使うほうが解りやすくなります。
```haskell
isZeroStr :: (Eq a ,Show a, Num a) => a -> String
isZeroStr 0 = "It is zero"
isZeroStr x = show x ++ " is not zero"
```
上記の`isZeroStr`の例では、0にマッチした結果で変数`x`を束縛していますが、マッチしなかった値を使わない場合は、ワイルドカード`_`を使う事ができます。
```haskell
isZero2 :: (Eq a ,Num a) => a -> Bool
isZero2 0 = True
isZero2 _ = False
```
ここで、型変数 a に`Eq`, `Show`, `Num`型クラスを指定しています。0がNumクラスのインスタンスになるのは良いでしょう、`Eq`はパターンマッチ、`Show`は`show`関数を使うための制約です。

リテラルやデータコンストラクタ(TrueやFalseをデータコンストラクタと呼ぶのでした)はそのままパターンマッチで使う事ができます。  
例えば、`True`なら1、`False`なら0を返す関数は次のように定義できます。
```haskell
trueToZero :: Num a => Bool -> a
trueToZero True  = 1
trueToZero False = 0
```
少しややこしいですが、`Int`や`Double`のような、リテラルに対するパターンマッチには、`Eq`型クラスの制約が必要です。
しかし、`Bool`型のパターンマッチには、型クラスの制約は必要ありません。（というか型変数ではないのでそもそもできません。）
詳しい話はややこしくなるのでしませんが、リテラルに対するパターンマッチは少し特殊なのです。

`Int`や`Char`のように、具体的な型を指定した場合は、Eq型クラスのインスタンスである事が解っているので、そのままパターンマッチに使う事ができます。
```haskell
tToTrue :: Char -> Bool
tToTrue 't' = True
tToTrue _   = False
```
ここまで、たかだか２パターンのパターンマッチを並べてきましたが、３つ以上のパターンを記述する事もできます。  
基本的には、上から順にチェックしていって、一案最初にマッチした式を計算します。
```haskell
nihongo :: Integer -> String
nihongo 0 = "Rei"
nihongo 1 = "Iti"
nihongo 2 = "Ni"
nihongo 3 = "San"
nihongo 4 = "Shi"
nihongo 5 = "Go"
nihongo _ = "Takusan"
```

####タプルのパターンマッチ

二値のタプルを受け取って、二つの値を加算して返す`addTuple`関数を考えてみましょう。  
まずは型を定義します。
```haskell
addTuple :: (Integer, Integer) -> Integer
```
Numクラスのインスタンスであれば加算する事ができるので、Integerの部分は多相にする事ができますね。
```haskell
addTuple :: (Eq a, Num a) => (a, a) -> a
```
以下のように、fst関数とsnd関数を使った実装を考える事ができます。
```haskell
addTuple :: Num a => (a, a) -> a
addTuple x = fst x + snd x
```
タプルの各要素に対してパターンマッチする事ができます。  
パターンマッチを使って実装したaddTupleはより意図が明確で解りやすいです。
```haskell
addTuple :: (Eq a, Num a) => (a, a) -> a
addTuple (x, y) = x + y
```

（問）４要素のタプルから値をそれぞれ取り出す関数 `f1` `f2` `f3` `f4`を実装してみましょう。

（問）次のような型を持った関数addPareの実装について考えてみましょう。
```haskell
addPare :: (Eq a, Num a, Eq b, Num b) => (a, b) -> (a, b) -> (a, b)
```

####リストのパターンマッチ

リストのパターンマッチには`(x:xs)`というパターンを使います。  
`:`の左辺の変数はリストの一番前の要素、右辺は残りの要素のリストに束縛されます。
そのため、リストの戦闘を取り出すhead関数、tail関数は次のようにして実装できます。
```haskell
myhead :: [a] -> a
myhead (x:_) = x

mytail :: [a] -> [a]
mytail (_:xs) = xs
```
しかし、`(x:xs)`パターンは、空リストにはマッチしません。
そのため、これらの関数に空リストを適用すると、エラーになってしまいます。
```haskell
*Main> myhead []
*** Exception: Sample.hs:21:1-16: Non-exhaustive patterns in function myhead
*Main> mytail []
*** Exception: Sample.hs:24:1-18: Non-exhaustive patterns in function mytail
```
エラーを回避するためには、必ず空リストとマッチするパターンを用意する必要があります。  
例えば、空リストが引数として渡された場合は空リストを返す、`safetail`関数は次のようになります。
```haskell
safetail :: [a] -> [a] 
safetail []     = [] 
safetail (_:xs) = xs 
```
（問）次の型を持つ`safehead`を実装してみましょう。
```haskell
safehead :: [a] -> a -> a
```

-------------------------------------------------

####Asパターン
リストをパターンマッチした場合、戦闘の要素と残りの要素に別れてしまいますが、実際には分割する前のリストを計算に使いたい場合もあります。  
そういう時のために、HaskellにはAsパターンという構文が用意されています。

例えば、文字列の一文字目が`'A'`だった場合のみ、文字列をそのまま返し、それ以外の場合空文字列を返したい場合、次のように書けば良いでしょう。

```haskell
headIsA :: String -> String
headIsA []        = ""
headIsA x@('A':_) = x
headIsA _         = ""
```

-------------------------------------------------

###ガード

パターンマッチでは、「値に何か関数を適用した結果」を条件分岐に使う事はできませんでした。  
そのような分岐がしたい場合は、ガードを使います。
```haskell
f :: (Ord a, Num a, Show a) => a -> String
f x
  | 0    <= x && x < 100  = "0 <= "    ++ show x ++ " < 100"
  | 100  <= x && x < 1000 = "100 <= "  ++ show x ++ " < 1000"
  | 1000 <= x             = "1000 <= " ++ show x
  | otherwise             = show x ++ " > 0"
```
二値を比較し、大きい値を返す`mymax`関数、小さい値を返す`mymin`関数を、ガードを使って実装すると次のようになります。
```haskell
mymax :: Ord a => a -> a -> a
mymax a b
  | a < b     = b
  | otherwise = a

mymin :: Ord a => a -> a -> a
mymin a b
  | a < b     = a
  | otherwise = b
```

-------------------------------------------------

###whereとlet in
関数型プログラミングは、宣言的に小さな関数をいくつも作って、大きな関数を組み立てていくのが基本的なスタイルです。  
しかし、一つの関数を定義するための、その関数専用の小さな関数を全て同じスコープで宣言すると、関数の数が増えて煩雑になりすぎるおそれがあるため、「ある関数からのみ呼び出す事ができる関数」を宣言するための構文が用意されています。
####where
関数の実装の後ろに`where`と書く事によって、その関数内で使いたい局所的な関数を宣言することができます。
```haskell
f :: (Show a, Show b) => (a, b) -> String
f (x, y) = l x ++ " and " ++ r y
  where
    l :: Show a => a -> String
    l v = "first value is " ++ show v

    r :: Show a => a -> String
    r v = "second value is " ++ show v
```
####let in
実装より前に、その型クラス内で局所的な関数を宣言したい場合は、`let in`を使うと良いでしょう。
```haskell
f :: (Show a, Show b) => (a, b) -> String
f (x, y) = let

  l :: Show a => a -> String
  l v = "first value is " ++ show v

  r :: Show a => a -> String
  r v = "second value is " ++ show v

  in l x ++ " and " ++ r y
```
また、`let in`はさらに局所的に使うことができます。
```haskell
Prelude> 10 + (let f x = x * 2 in f 5) + 10
30
```
-------------------------------------------------

###case式

パターンマッチの構文の一つに、case式というものがあります。
```haskell
case 式 of
  パターン -> 式
  パターン -> 式
  パターン -> 式
  ...
```
mymax関数をcase式を用いた形に書きかえると、次のようになります。
```haskell
mymax :: Ord a => a -> a -> a
mymax a b = case a < b of
  True -> b
  _    -> a
```